 5  TEXT : GOSUB 1450: VTAB 13: HTAB 8
 10  D$ =  CHR$ (4):SLOT = 0
 20  REM  '''GET SLOT NUMBER
 30  GOSUB 1450
 40  VTAB 21: GOSUB 1400: INPUT "APPLE II VGA CARD SLOT (1-7): ";SLOT
 50  IF SLOT < 1 THEN  GOTO 1500
 60  IF SLOT > 7 THEN  GOTO 1500
 70  BASEADDR = 49280 + 16 * SLOT
 100  REM '''MAIN MENU
 110  GOSUB 1450
 120  VTAB 10: GOSUB 1400: HTAB 4: PRINT "CONFIGURATION OPTIONS FOR SLOT ";SLOT;"": GOSUB 1400: PRINT ""
 130  PRINT "1  SCANLINES  ON      2  SCANLINES  OFF"
 140  PRINT "3  MONOCHROME ON      4  MONOCHROME OFF"
 150  PRINT "5  WHITE MONOCHROME   6  GREEN MONOCHROME"
 160  PRINT "7  AMBER MONOCHROME   8  CHARACTER TEST"
 170  PRINT "9  RESET CONFIG       10 COLOR PATTERN"
 180  PRINT "11 ABOUT              12 QUIT": PRINT ""
 190  VTAB 21: GOSUB 1400: INPUT "PLEASE ENTER YOUR SELECTION: ";CH
 200  IF CH < 1 THEN  GOSUB 1550
 210  IF CH > 12 THEN  GOSUB 1550
 220  IF CH = 1 THEN  GOSUB 500
 230  IF CH = 2 THEN  GOSUB 550
 240  IF CH = 3 THEN  GOSUB 600
 250  IF CH = 4 THEN  GOSUB 650
 260  IF CH = 5 THEN  GOSUB 700
 270  IF CH = 6 THEN  GOSUB 750
 280  IF CH = 7 THEN  GOSUB 800
 290  IF CH = 8 THEN  GOSUB 900
 300  IF CH = 9 THEN  GOSUB 1000
 310  IF CH = 10 THEN  GOSUB 850
 320  IF CH = 11 THEN  GOSUB 1650
 330  IF CH = 12 THEN  GOSUB 2000
 340  GOTO 100
 500  REM  '''SCANLINES ON
 510  POKE BASEADDR + 0,1: RETURN 
 550  REM  '''SCANLINES OFF
 560  POKE BASEADDR + 0,2: RETURN 
 600  REM  '''MONOCHROME ON
 610  POKE 49185,128: RETURN 
 650  REM  '''MONOCHROME OFF
 660  POKE 49185,0: RETURN 
 700  REM  '''MONOCHROME WHITE TEXT
 710  GOSUB 600
 720  POKE BASEADDR + 1,1
 730  RETURN 
 750  REM  '''MONOCHROME GREEN TEXT
 760  GOSUB 600
 770  POKE BASEADDR + 1,2
 780  RETURN 
 800  REM  '''MONOCHROME AMBER TEXT
 810  GOSUB 600
 820  POKE BASEADDR + 1,3
 830  RETURN 
 850  REM  '''COLOR PATTERN
 860  GR : HOME 
 870  FOR I = 0 TO 31: COLOR= I / 2: VLIN 0,39 AT I: NEXT I: FOR I = 0 TO 14 STEP 2: PRINT  TAB( I * 2 + 1);I;: NEXT I: PRINT : FOR I = 1 TO 15 STEP 2: PRINT  TAB( I * 2 + 1);I;: NEXT I
 880  PRINT "": GET A$: TEXT : RETURN 
 900  REM  '''CHARACTER SET TEST
 910  HOME 
 920  FOR R = 0 TO 7
 930  FOR C = 0 TO 31
 940  POKE R * 128 + 1024 + C,R * 32 + C
 950  NEXT C,R
 960  VTAB 21: GOSUB 1400: PRINT "PRESS ANY KEY TO CONTINUE...": GET A$
 970  RETURN 
 1000  REM  '''RESET CONFIGURATION
 1010  POKE BASEADDR + 4,1
 1020  RETURN
 1400  REM  STRING ROUTINES
 1410  PRINT "---------------------------------------": RETURN 
 1450  HOME : PRINT "APPLE II VGA": GOSUB 1400: PRINT "COPYRIGHT (C) 2021-2023 MARK AIKENS": PRINT "COPYRIGHT (C) 2022-2023 DAVID KUDER": RETURN 
 1500  REM  '''ERROR HANDLING ROUTINES
 1510  GOSUB 1450: VTAB 13: HTAB 13: PRINT "[INVALID SLOT]"
 1520  GOSUB 1600: GOTO 20
 1550  GOSUB 1450: VTAB 13: HTAB 11: PRINT "[INVALID OPTION]"
 1560  GOSUB 1600: RETURN 
 1600  FOR I = 1 TO 1000: NEXT I: RETURN 
 1650  GOSUB 1450: PRINT "": PRINT "": PRINT "THIS PROJECT IS A VGA CARD FOR APPLE II": PRINT "COMPUTERS TO  OUPUT A CRISP  RGB SIGNAL": PRINT "TO A VGA MONITOR  INSTEAD OF  HAVING TO": PRINT "RELY ON  THE COMPOSITE OUTPUT.  THIS IS"
 1660  PRINT "ACCOMPLISHED BY  SNOOPING THE  6502 BUS": PRINT "AND CREATING A SHADOW COPY OF THE VIDEO": PRINT "MEMORY WITHIN A RASPBERRY PI PICO, THEN": PRINT "PROCESSING THE VIDEO MEMORY CONTENTS TO"
 1670  PRINT "OUTPUT A 'PERFECT' SIGNAL.": PRINT "": PRINT "": HTAB 6: PRINT "[MORE DETAILS AVAILABLE AT]": PRINT "HTTPS://GITHUB.COM/MARKADEV/APPLEII-VGA"
 1680  VTAB 21: GOSUB 1400: PRINT "PRESS ANY KEY TO CONTINUE...": GET A$
 1690  RETURN 
 2000  HOME : PRINT "YOU MAY NOW LOAD YOUR SOFTWARE USING": PRINT "PRODOS OR THE APPROPRIATE PR# COMMAND.": PRINT "SETTINGS WILL PERSIST UNTIL THE SYSTEM": PRINT "IS REBOOTED.": END
